10 DEFINT A-Z:DIM B(9,9),BT(15),BTN(15),MX(9),MY(9),MG(9),ST(81,3):SP=0:SL=0
20 FOR I=0 TO 14:BT(I)=2^I:BTN(I)=NOT(BT(I)):NEXT I
30 PRINT "Input sudoku":DIM BI(9):FOR Y=0 TO 8
40 PRINT Y+1;:INPUT ": ",BI(0),BI(1),BI(2),BI(3),BI(4),BI(5),BI(6),BI(7),BI(8)
50 FOR X=0 TO 8:N=BI(X):B(Y,X)=N:IF N>9 OR N<0 THEN PRINT "Invalid number at row";Y+1;", column";X+1:END
51 G=(Y\3)*3+(X\3):IF N=0 THEN ST(SL,0)=(G*256) OR (Y*16) OR X:SL=SL+1:GOTO 90
60 M=BT(N):DUP=(M AND MX(X))OR(M AND MY(Y))OR(M AND MG(G))
61 IF DUP<>0 THEN PRINT "Duplicated number at row";Y+1;", column";X+1:END
70 MX(X)=MX(X) OR M:MY(Y)=MY(Y) OR M:MG(G)=MG(G) OR M
90 NEXT X:NEXT Y:PRINT "Calculating...";
100 RN=1:WHILE RN=1 AND SL>0:RN=0:SP0=0
110 FOR SP=0 TO SL-1:DT=ST(SP,0)
111 G=(DT\256) AND 15:Y=(DT\16) AND 15:X=DT AND 15
120 M=NOT(MX(X) OR MY(Y) OR MG(G)) AND &H3FE
121 IF M=0 THEN PRINT "No answer at row";Y+1;", column";X+1:END
180 IF (M AND (M-1))<>0 THEN ST(SP0,0)=ST(SP,0):SP0=SP0+1:GOTO 200
190 RN=1:N=M:GOSUB 600:B(Y,X)=N
191 MX(X)=MX(X) OR M:MY(Y)=MY(Y) OR M:MG(G)=MG(G) OR M
200 NEXT SP:SL=SP0:WEND
201 IF SL=0 THEN GOSUB 500:END
210 FOR SP=0 TO SL-1:DT=ST(SP,0)
211 G=DT\256 AND 15:Y=(DT\16) AND 15:X=DT AND 15
220 ST(SP,1)=NOT(MX(X) OR MY(Y) OR MG(G)) AND &H3FE:ST(SP,2)=0
230 NEXT SP
231 FOR I=0 TO 9:MX(I)=0:MY(I)=0:MG(I)=0:NEXT I:SP=0
240 WHILE SP<SL:N=ST(SP,2):DT=ST(SP,0)
241 G=DT\256 AND 15:Y=(DT\16) AND 15:X=DT AND 15
270 IF N<>0 THEN MG(G)=MG(G) AND BTN(N):MX(X)=MX(X) AND BTN(N):MY(Y)=MY(Y) AND BTN(N)
280 BM=ST(SP,1) AND (NOT(MG(G) OR MX(X) OR MY(Y))) AND &H3FE:GOSUB 700
290 B(Y,X)=N:ST(SP,2)=N:IF N=0 THEN IF SP>0 THEN SP=SP-1:GOTO 400 ELSE PRINT "No answer.":END
300 MG(G)=MG(G) OR BT(N):MX(X)=MX(X) OR BT(N):MY(Y)=MY(Y) OR BT(N)
320 SP=SP+1:ST(SP,2)=0
400 WEND:GOSUB 500:END
500 PRINT "Complete.":FOR Y=0 TO 8:FOR X=0 TO 8:N=B(Y,X)
510 PRINT USING "# ";B(Y,X);:IF (X MOD 3)=2 THEN PRINT " ";
520 NEXT X:PRINT "":IF (Y MOD 3)=2 THEN PRINT ""
530 NEXT Y:RETURN
600 FOR NB0=1 TO 9:IF (BT(NB0) AND N)<>0 THEN N=NB0:RETURN
610 NEXT NB0:N=0:RETURN
700 IF BM=0 THEN N=0:RETURN
710 WHILE N<9:N=N+1:IF (BT(N) AND BM)<>0 THEN RETURN
720 WEND:N=0:RETURN
