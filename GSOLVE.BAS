10 DEFINT A-Z:L=0:DIM B(15),G(8,2):FOR I=0 TO 14:B(I)=2^I:NEXT I
20 PRINT "Hint: Enter ";CHR$(34);"0,0,0";CHR$(34);" to start calculation."
30 WHILE L<8
40 INPUT "Guess,A,B:",IG,A,B:IF IG=0 THEN GOTO 100
50 NR=IG:GOSUB 400:IF RT=0 OR A<0 OR A>3 OR B<0 OR B>4 OR (A+B)>4 THEN PRINT "Invalid Input!":GOTO 40
60 G(L,0)=IG:G(L,1)=A*10+B:L=L+1:WEND
100 PRINT "":PRINT "Calculation started, press Esc to exit."
120 FOR N=123 TO 9876:IF CHR$(27)=INKEY$ THEN PRINT "":END
130 FOR I=0 TO L-1
140 NA=N:NB=G(I,0):GOSUB 300:IF RT<>G(I,1) THEN GOTO 200
150 NEXT I
160 IF N<=999 THEN PRINT " 0";:PRINT USING "###";N;ELSE PRINT USING "#####";N;
200 NEXT N:PRINT "":PRINT "":END
300 RT=0:M=0:MA=0:MB=0
310 FOR I0=0 TO 3:DA=NA MOD 10:DB=NB MOD 10
320 IF (B(DA) AND M)<>0 THEN RT=-1:RETURN ELSE M=M OR B(DA)
330 IF DA=DB THEN RT=RT+10 ELSE MA=MA OR B(DA):MB=MB OR B(DB)
340 NA=NA\10:NB=NB\10:NEXT I0:M=MA AND MB
350 WHILE M<>0:M=M AND (M-1):RT=RT+1:WEND
360 RETURN
400 RT=1:M=0:FOR I0=0 TO 3:DI=NR MOD 10
410 IF (B(DI) AND M)<>0 THEN RT=0:RETURN
420 M=M OR B(DI):NR=NR\10:NEXT I0
430 RETURN
