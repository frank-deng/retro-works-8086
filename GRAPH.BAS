10 DEFINT A-Z:KEY OFF:PRINT "*** Graph Drawing ***":PRINT ""
20 L=0:LT=0:S#=0:M#=0:Y=1:DIM LABEL$(2048),VALUE#(2048)
30 PRINT "Hint: Input a single comma to start drawing."
40 WHILE 1: INPUT "Label,Value: ",T$,D$: IF T$="" OR D$="" THEN GOTO 80
50 D#=VAL(D$): L=L+1: S#=S#+D#:IF D#>M# THEN M#=D#
60 K=LEN(T$)+9:IF K>LT THEN LT=K
62 LABEL$(L)=T$:VALUE#(L)=D#
70 WEND
80 SCREEN 2:CLS:W=638-LT*8
82 FOR I=1 TO L:D#=VALUE#(I)
90 LOCATE Y,1,0:PRINT LABEL$(I);
100 LOCATE Y,LT-8:PRINT "(";:PRINT USING "##.###%";D#/S#*100;:PRINT ")";
110 K!=D#/M#:LINE (LT*8+1,(Y-1)*8)-(K!*W+LT*8+1,(Y-1)*8+6),,BF
120 IF Y<24 THEN Y=Y+1:GOTO 150
130 LOCATE 25,1,0:PRINT "Press any key to continue";
140 WHILE INPUT$(1)="":WEND:CLS:Y=1
150 NEXT I:WHILE INPUT$(1)="":WEND:CLS
160 IF L>24 OR L<2 THEN SCREEN 0:END
170 DIM PAT$(16):GOSUB 400
180 PI#=3.141592654:PI2#=PI#*2:Y=(25-L)/2:K1#=0:K2#=VALUE#(1)
200 FOR I=1 TO L:LOCATE Y+1,3,0:PRINT LABEL$(I):D#=VALUE#(I)
201 LINE (0,Y*8)-(14,Y*8+6),1,B:COL=(I-2) MOD 14
202 LOCATE Y+1,LT-8+2:PRINT "(";:PRINT USING "##.###%";D#/S#*100;:PRINT ")";
210 IF I>1 THEN PAINT(3,Y*8+3),PAT$(COL)
220 AB#=K1#/S#*PI2#:IF I=L THEN AE#=PI2# ELSE AE#=K2#/S#*PI2#
230 IF ABS(AB#)<0.00001 THEN AB#=0.00001
240 CIRCLE (430,100),190,1,-AB#,-AE#,0.42
250 XP=430+(140)*COS((AB#+AE#)/2):YP=100-(70)*SIN((AB#+AE#)/2)
260 IF I>1 THEN PAINT(XP,YP),PAT$(COL)
270 K1#=K1#+VALUE#(I):K2#=K2#+VALUE#(I+1)
280 Y=Y+1:NEXT I:WHILE INPUT$(1)="":WEND
300 CLS:SCREEN 0:END
400 FOR I=0 TO 13:PT$=""
410 READ C:FOR J=1 TO C
420 READ N:PT$=PT$+CHR$(N)
430 NEXT J:PAT$(I)=PT$
440 NEXT I:RETURN
450 DATA 1,255:DATA 2,170,85:DATA 2,255,170:DATA 2,0,170
460 DATA 4,136,68,34,17:DATA 4,17,34,68,136
470 DATA 8,128,65,34,20,8,20,34,65
480 DATA 6,136,68,34,17,34,68
490 DATA 3,68,255,68
500 DATA 4,119,187,221,238
510 DATA 4,238,221,187,119
520 DATA 8,127,190,221,235,247,235,221,190
530 DATA 6,119,187,221,238,221,187
540 DATA 3,187,187,0

