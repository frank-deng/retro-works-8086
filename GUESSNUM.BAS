10 RANDOMIZE TIMER:KEY OFF:WIDTH 40:DEFINT A-Z:DIM A(4),P(4)
20 CLS:LOCATE 4,10,1:PRINT "*** Bulls and Cows ***";
30 GOSUB 200:T=0:E=0
40 WHILE E=0:LOCATE 6,15:PRINT SPC(24);
50 LOCATE 6,15:PRINT "Guess: ":GOSUB 400
60 IF E=2 THEN GOTO 100
70 GOSUB 300
80 T=T+1:LOCATE 8+T,14:PRINT STR$(T);". ";:PRINT USING "#";P(0);P(1);P(2);P(3);:PRINT USING " #A#B";A;B
90 IF A=4 AND B=0 THEN E=-1 ELSE IF T>=8 THEN E=1
100 WEND
110 IF E>0 THEN LOCATE 19,7:PRINT "You Lose! The answer is: ";:PRINT USING "#";A(0);A(1);A(2);A(3);:SOUND 100,2 ELSE LOCATE 19,17:PRINT "You win!";:SOUND 500,1:SOUND 1000,1
120 LOCATE 21,13:PRINT "Play again? (Y/N)";
130 K$=INPUT$(1):IF K$="y" OR K$="Y" THEN 20 ELSE IF K$="n" OR K$="N" THEN 140 ELSE 130
140 CLS:WIDTH 80:END
200 FOR I=0 TO 3
210 N=INT((RND*100) MOD 10)
220 FOR J=0 TO I-1
230 IF A(J)=N THEN GOTO 210
240 NEXT J
250 A(I)=N:NEXT I:RETURN
300 A=0:B=0:FOR I=0 TO 3
310 IF P(I)=A(I) THEN A=A+1
320 NEXT I
330 FOR I=0 TO 3:FOR J=0 TO 3
340 IF I<>J AND P(I)=A(J) THEN B=B+1
350 NEXT J:NEXT I:RETURN
400 L=0:LOCATE 6,22
410 LOCATE 6,22+L:KI$=INPUT$(1)
421 K=ASC(KI$)
422 IF K=27 THEN E=2:RETURN
423 IF K=13 AND L=4 THEN E=0:RETURN
424 IF K=8 AND L>0 THEN L=L-1:LOCATE 6,22+L:PRINT " ";:LOCATE 6,22+L:GOTO 410
425 IF K>=48 AND K<=57 AND L<4 THEN GOTO 430 ELSE GOTO 410
430 NUM=K-48:FOR I=0 TO L-1
450 IF P(I)=NUM THEN GOTO 410
460 NEXT I
470 PRINT CHR$(K);:P(L)=NUM:L=L+1
499 GOTO 410

